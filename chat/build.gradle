apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka-android'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

def skygearChatVersion = "git describe --abbrev=0 --tags".execute().text.trim()
def skygearChatFullVersion = "git describe --always --tags".execute().text.trim()

group = 'io.skygear.plugins'
version = skygearChatFullVersion

def bintrayUser = System.getenv('BINTRAY_USER')
def bintrayApiKey = System.getenv('BINTRAY_API_KEY')

if (project.rootProject.file('local.properties').exists()) {
    Properties localProperties = new Properties()
    localProperties.load(project.rootProject.file('local.properties').newDataInputStream())

    bintrayUser = localProperties.getProperty('bintrayUser', bintrayUser)
    bintrayApiKey = localProperties.getProperty('bintrayApiKey', bintrayApiKey)
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName skygearChatFullVersion
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile 'com.android.support:support-annotations:25.3.1'
    compile 'joda-time:joda-time:2.9.4'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.github.stfalcon:chatkit:0.2.2'
    compile project(path: ':photoview')
    compile 'com.dewarder:holdingbutton:0.0.9'

    compile 'io.skygear:skygear:1.1.0-alpha.0'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
}

//
// TODO: Generate Javadoc and Kotlin Doc using dokka
//
// task generateSourcesJar(type: Jar) {
//     from android.sourceSets.main.java.srcDirs
//     classifier 'sources'
// }
//
// task generateJavadocs(type: Javadoc) {
//     source = android.sourceSets.main.java.srcDirs
//     classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
// }
//
// afterEvaluate {
//     generateJavadocs.classpath += files(android.libraryVariants.collect { variant ->
//         variant.javaCompile.classpath.files
//     })
// }
//
// task generateJavadocsJar(type: Jar) {
//     from generateJavadocs.destinationDir
//     classifier 'javadoc'
// }
//
// generateJavadocsJar.dependsOn generateJavadocs
// artifacts {
//     archives generateJavadocsJar
//     archives generateSourcesJar
// }

bintray {
    user = bintrayUser
    key = bintrayApiKey
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'skygear-chat-android'
        userOrg = 'skygeario'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/SkygearIO/chat-SDK-Android.git'
        publish = true
        version {
            name = skygearChatFullVersion
            desc = "Skygear Android SDK ${skygearChatVersion}"
            released  = new Date()
            vcsTag = skygearChatVersion
        }
    }
}
repositories {
    mavenCentral()
}
